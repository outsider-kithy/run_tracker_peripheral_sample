#include <M5Unified.h>
#include "getSteps.h"
#include "getTime.h"
#include "getGps.h"
#include "jsonHandler.h"
#include "bleHandler.h"
#include "connectWifi.h"
#include "manageBattery.h"

// Aボタンを押した回数
int pressCount = 0;

// ディスプレイをオフにする時間
unsigned long lastOperationMillis = 0;
const unsigned long SLEEP_TIMEOUT = 30000; // 30秒

void setup() {
  connectWifi();
  
  M5.begin();

  // すでにM5 Stickに保存されているJSONファイルを表示
  listFilesFromRoot();
  
  // バッテリー節約のため、画面の明るさを落とす
  M5.Display.setBrightness(32);
  // CPU周波数を160MHzに設定
  setCpuFrequencyMhz(160);

  // 起動時を「最後に操作した時刻」とする
  lastOperationMillis = millis();
  
  M5.Lcd.setRotation(3);
  M5.Lcd.setTextSize(2);
  M5.Lcd.setCursor(0,0);
  M5.Lcd.println("Initializing...");

  // バッテリー残量を表示
  setBatteryCharge();

  //歩数カウントを初期化
  setupSteps();
  //タイマーを初期化
  setupTimer();
  //GPSを初期化
  setupGPS();
  // BLE初期化
  initBLE(); 
  M5.Lcd.setCursor(0, 20);
  M5.Lcd.println("Press A button!");

}

void loop() {
  	M5.update();
	updateGPS();
	//　バッテリー残量を更新
	updateBatteryCharge();
	
	// 最後のボタン操作から30秒で画面オフ
	if (millis() - lastOperationMillis >= SLEEP_TIMEOUT) {
		previousSaveMillis = millis();
		displayOff();
	}

	// Aボタンが押された瞬間
	if (M5.BtnA.wasPressed()) {
		// 最終操作時刻を更新
    	lastOperationMillis = millis();
		displayOn();
		setBatteryCharge();

		// 押された回数を増やす
		pressCount++;
		// 1回目
		if (pressCount == 1) {
			M5.Lcd.setCursor(0, 40);
			M5.Lcd.setTextColor(YELLOW);
			M5.Lcd.println("Tracking Start!");
			//歩数カウントスタート
			startCountSteps();
			//タイマースタート
			startTimer();
			//GPSスタート
			startGPS();
		}

		// 2回目
		else if (pressCount == 2) {
			// 歩数カウントストップ
			stopCountSteps();
			// タイマーストップ
			stopTimer();
			// GPSストップ
			stopGPS();
			// 走行データをJSONに保存
			saveRunDataToFile(path, totalDistance, steps, elapsed);
			M5.Lcd.setCursor(0, 100);
			M5.Lcd.setTextColor(RED);
			M5.Lcd.println("Data was saved!");
		}
		// 3回目(画面とデータをリセット)
		else if (pressCount == 3) {

			pressCount = 0;
			path.clear();
			totalDistance = 0.0;
			steps = 0;
			elapsed = 0;
			startDate, endDate = "";

			// ディープスリープして1秒後に再起動
			esp_sleep_enable_timer_wakeup(1000000);
			esp_deep_sleep_start();
			
		}	
	}

	// セントラルからSYNCコマンドが送られてきたら
	if (syncRequested) {
        syncRequested = false;
        txCharacteristic->setValue("READY");
        txCharacteristic->notify();
        delay(100);
        sendFileWithAck();
    }

	// セントラルからDELETEコマンドが送られてきたら
    if(deleteRequested){
		txCharacteristic->setValue("DELETE_processing...");
		txCharacteristic->notify();
		delay(100);
		deleteAllJsonFiles();
    }

	// Bボタンが押されたら、キャリブレーションを実行
	if (M5.BtnB.wasPressed()) {
		calibrateStepThreshold();
	}
}

